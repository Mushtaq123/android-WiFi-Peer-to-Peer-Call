//apply plugin: 'io.fabric'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

def PROJECT_NAME = "WifiVoiceCall"
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 2

def VERSION_CODE = commitCount()
def VERSION_NAME = "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

def APK_NAME = PROJECT_NAME + "-" + VERSION_NAME + ".apk"

//buildscript {
//    repositories {
//        maven { url 'https://maven.fabric.io/public' }
//    }
//
//    dependencies {
//        classpath 'io.fabric.tools:gradle:1.+'
//    }
//}
//
//repositories {
//    maven { url 'https://maven.fabric.io/public' }
//}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.android.wificall"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode VERSION_CODE
        versionName VERSION_NAME
    }

    signingConfigs {
        debug {
            try {
                storeFile file("keys/debug.keystore")
                storePassword DEBUG_KEYSTORE_PASSWORD
                keyAlias "androiddebugkey"
                keyPassword DEBUG_KEY_PASSWORD
            } catch (ex) {
                throw new InvalidUserDataException("You should define DEBUG_KEYSTORE_PASSWORD and DEBUG_KEY_PASSWORD in gradle.properties.")
            }
        }
        release {
            try {
                storeFile file("keys/wificall.release.jks")
                storePassword RELEASE_KEYSTORE_PASSWORD
                keyAlias "wificallalias"
                keyPassword RELEASE_KEY_PASSWORD
            } catch (ex) {
                throw new InvalidUserDataException("You should define RELEASE_KEYSTORE_PASSWORD and RELEASE_KEY_PASSWORD in gradle.properties.")
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
            minifyEnabled false
            shrinkResources false
            archivesBaseName = APK_NAME;
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled false
            shrinkResources true
            archivesBaseName = APK_NAME;
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.buildType.name.equals("debug")) {
                output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace("app-debug.apk", APK_NAME))
            } else {
                output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace("app-release.apk", APK_NAME))
            }
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'

    compile 'com.jakewharton:butterknife:8.1.0'
    apt 'com.jakewharton:butterknife-compiler:8.1.0'
    compile 'org.greenrobot:eventbus:3.0.0'

    compile 'com.pixplicity.easyprefs:library:1.8.1@aar'
    compile 'com.github.amlcurran.showcaseview:library:5.4.3'

    compile 'io.reactivex.rxjava2:rxjava:2.0.0-RC1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.0-RC1'
//    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
//        transitive = true;
//    }
}

def commitCount() {
    def stdout = new ByteArrayOutputStream()
    description = "git rev-list --full-history --all"
    println(description)
    exec {
        commandLine 'git', 'rev-list', '--full-history', '--all'
        standardOutput = stdout
    }
    String output = stdout.toString();
    List<String> commits = output.split("\n");
    println("Version Code: " + commits.size())
    return commits.size();
}